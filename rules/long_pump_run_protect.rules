var Timer timer = null
rule "LongPumpRunProtect"
when
    Item mqtt_pump changed from OFF to ON
then
    logInfo("LongPumpRunProtectRule", "Pump switched on")
    if(timer === null) 
    {
        logInfo("LongPumpRunProtectRule", "Start pump protection timer")
        timer = createTimer( now.plusMinutes(3), [|
            logInfo("LongPumpRunProtectRule", "Pump protection timer elapsed\n")
            if ( mqtt_pump.state == ON )
            {
                logInfo("LongPumpRunProtectRule", "Switch off pump.\n")
                mqtt_pump.sendCommand(OFF) 
            }
            timer = null
        ])
    }
    else
    {
        logInfo("LongPumpRunProtectRule", "Reschedule pump protection timer")
        timer.reschedule(now.plusMinutes(3))
    }
end
